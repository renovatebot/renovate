plugins {
    id 'net.researchgate.release' version '2.8.1'
    // TODO: wait for https://github.com/hierynomus/license-gradle-plugin/issues/161
    // id "com.github.hierynomus.license" version "0.14.0" apply false
}

apply plugin: 'java'

description = "Notifications infrastucture for Apereo uPortal"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    // TODO: wait for https://github.com/hierynomus/license-gradle-plugin/issues/161
    // apply plugin: 'license'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testCompileOnly 'junit:junit:4.13.1'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.7.0'
    }

    // TODO: wait for https://github.com/hierynomus/license-gradle-plugin/issues/161
    // license {
    //     header = rootProject.file('docs/short-license-header.txt')
    //     strictCheck = true
    //     mapping {
    //         java = 'SLASHSTAR_STYLE'
    //     }
    //     exclude "**/*.json"
    // }

    sourceCompatibility = 1.8

    /* Release Management
     *
     * Based on Sonatype's guide for publishing w/ Gradle:  http://central.sonatype.org/pages/gradle.html
     */
    ext {
        publishingUsername = project.hasProperty('ossrhUsername') ? project.getProperty('ossrhUsername') : ""
        publishingPassword = project.hasProperty('ossrhPassword') ? project.getProperty('ossrhPassword') : ""
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }
    tasks.withType(Javadoc) {
        // FIXME: Fix errors rather than supressing them
        failOnError = false
    }
    tasks.withType(Jar) {
        from(project.projectDir) {
            include "${rootDir}/LICENSE"
            into 'META-INF'
        }
    }
    artifacts {
        archives javadocJar, sourcesJar
    }
    signing {
        required { gradle.taskGraph.hasTask('uploadArchives') }
        sign configurations.archives
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: publishingUsername, password: publishingPassword)
                }

                pom.project {
                    name 'NotificationPortlet'
                    description 'Content objects for displaying important notices from campus ' +
                            'systems in uPortal based on Web Components and JSR-286 (Portlet 2.0)'
                    url 'https://github.com/Jasig/NotificationPortlet'

                    scm {
                        connection 'scm:git:git://github.com/Jasig/NotificationPortlet.git'
                        url 'https://github.com/Jasig/NotificationPortlet'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            organization 'NotificationPortlet Developers'
                            organizationUrl 'https://github.com/Jasig/NotificationPortlet/graphs/contributors'
                        }
                    }
                }
            }
        }
    }
    release {
        tagTemplate = 'v${version}' // Looks like it should be a GString, but not necessary
        git {
            requireBranch = ''
        }
    }
    afterReleaseBuild.dependsOn uploadArchives
}
